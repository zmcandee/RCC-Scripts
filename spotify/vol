#!/usr/bin/env osascript


# vol          -- volume to 100
# vol xx       -- volume to xx
# vol up xx    -- volume up by xx
# vol dn xx    -- volume down by xx
# vol fd xx yy -- volume fade yy seconds to xx

on setVol(level)
	if level < 0 then
		tell application "Spotify" to set sound volume to 0
	else if level > 100 then
		tell application "Spotify" to set sound volume to 100
	else
		tell application "Spotify" to set sound volume to level
	end if
end setVol


# run is a special name that is called when the applescript loads and is passed any arguments (argv) from the 
# commandline if not opened like an app
on run argv
	# Set the defaults for the fade then override them if anything was passed on the commandline
	
	if (count of argv) is equal to 0 then
		setVol(100)
		return
	else if (item 1 of argv) is not in {"up", "dn", "fd", "down", "fade"} then
		setVol(item 1 of argv as number)
	else
		set mode to item 1 of argv
		if (count of argv) is equal to 2 then
			set targetVol to item 2 of argv
			set fadeSecs to 3
		else if (count of argv) >= 3 then
			set targetVol to item 2 of argv
			set fadeSecs to item 3 of argv
		else if mode is not in {"fd", "fade"} then
			set targetVol to 10
		else
			set targetVol to 100
			set fadeSecs to 3
		end if
		
		tell application "Spotify" to set currentVol to sound volume
		if mode is in {"up"} then
			setVol(currentVol + targetVol)
		else if mode is in {"dn", "down"} then
			setVol(currentVol - targetVol)
		else 
			if (targetVol as number) > (currentVol as number) then
				set fadeDelay to fadeSecs / (targetVol - currentVol) * 2
				set volInc to 2
			else
				set fadeDelay to fadeSecs / (currentVol - targetVol) * 2
				set volInc to -2
			end if

			repeat with newVol from currentVol to targetVol by volInc
				setVol(newVol)
				delay (fadeDelay)
			end repeat
		end if
	end if
end run

