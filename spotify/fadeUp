#!/usr/bin/env osascript

# run is a special name that is called when the applescript loads and is passed any arguments (argv) from the 
# commandline if not opened like an app
on run argv
	# Set the defaults for the fade then override them if anything was passed on the commandline
	set fadeSecs to 3
	set targetVol to 100
	
	if (count of argv) >= 2 then
		set targetVol to item 2 of argv
	end if
	if (count of argv) >= 1 then
		set fadeSecs to item 1 of argv
	end if

	tell application "Spotify"
		# currentVol is a variable we made to store the actual Spotify volume (sound volume)
		set currentVol to sound volume
		
		# We if we are currently louder than the target volume bail out
		if (currentVol as number) >= (targetVol as number) then return 
		
		# Find the actual delay length for each step based on total fade time divided by the number of steps
		set fadeDelay to fadeSecs / (targetVol - currentVol) * 2

		repeat with newVol from currentVol to targetVol by 2
			# Spotify volume will wrap around (ie 102 will set the volume to 2 and -1 will set it to 99)
			# To prevent volume glitches make sure we aren't crossing 0 or 100
			if newVol > 100 then
				set sound volume to 100
			else
				set sound volume to newVol
			end if
			delay (fadeDelay)
		end repeat
	end tell
end run
